find_package(Doxygen QUIET)

if (NOT DOXYGEN_FOUND)
  message(STATUS "Doxygen was not found. Docs will not be built.")
  return()
endif()

get_target_property(CURVY_DOCS_HEADERS curvy PUBLIC_HEADER)

set(CURVY_DOCS_INPUT ${CMAKE_CURRENT_LIST_DIR}/../../include)
set(CURVY_DOCS_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
set(CURVY_DOCS_INDEX_FILE ${CURVY_DOCS_OUTPUT_DIRECTORY}/html/index.html)
set(CURVY_DOCS_DOXYFILE_IN ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in)
set(CURVY_DOCS_DOXYFILE_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)

configure_file(${CURVY_DOCS_DOXYFILE_IN} ${CURVY_DOCS_DOXYFILE_OUT} @ONLY)
file(MAKE_DIRECTORY ${CURVY_DOCS_OUTPUT_DIRECTORY})

add_custom_command(OUTPUT ${CURVY_DOCS_INDEX_FILE}
  DEPENDS ${CURVY_DOCS_HEADERS}
  COMMAND ${DOXYGEN_EXECUTABLE} ${CURVY_DOCS_DOXYFILE_OUT}
  MAIN_DEPENDENCY ${CURVY_DOCS_DOXYFILE_OUT} ${CURVY_DOCS_DOXYFILE_IN}
  COMMENT "Generating docs")

add_custom_target(docs ALL DEPENDS ${CURVY_DOCS_INDEX_FILE})